tabular-json
   table_contents
   element

value
   object
   array
   table
   string
   number
   "true"
   "false"
   "null"

object
    '{' ws '}'
    '{' members '}'

members
    member
    member ',' members

member
    ws string ws ':' element

array
    '[' ws ']'
    '[' elements ']'

elements
    element
    element ',' elements

element
    ws value ws

table
    '---' wst newline table_contents newline wst '---'

table_contents
    header newline rows

header
    field
    field ',' header

rows
    row
    row newline rows

field
    wst string wst
    wst string wst '.' field

row
    wst value wst
    wst value wst ',' row

string
    '"' characters '"'

characters
    ""
    character characters

character
    any_character - '"' - '\'
    '\' escape

escape
    '"'
    '\'
    '/'
    'b'
    'f'
    'n'
    'r'
    't'
    'u' hex hex hex hex

hex
    digit
    'A' . 'F'
    'a' . 'f'

number
    "inf"
    "-inf"
    "nan"
    integer fraction exponent

integer
    digit
    onenine digits
    '-' digit
    '-' onenine digits

digits
    digit
    digit digits

digit
    '0'
    onenine

onenine
    '1' . '9'

fraction
    ""
    '.' digits

exponent
    ""
    'E' sign digits
    'e' sign digits

sign
    ""
    '+'
    '-'

comment
    singleline_comment
    multiline_comment

singleline_comment
    "//" slc_characters

slc_characters
    ""
    slc_character
    slc_character slc_characters

slc_character
    any_character - newline

multiline_comment
    "/*" mlc_characters "*/"

mlc_characters
    ""
    mlc_character
    mlc_character mlc_characters

mlc_character
    any_character - "*"
    "*" mlc_non_escape

mlc_non_escape
    any_character - '/'

any_character
    '0020' . '10FFFF'

ws
    ""
    space ws
    tab ws
    newline ws
    carriagereturn ws
    comment ws

wst
    ""
    space wst
    tab wst
    carriagereturn wst
    comment wst

space
    '0020'

tab
    '0009'

newline
    '000A'

carriagereturn
    '000D'
