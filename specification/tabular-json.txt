tabular-json
   element

value
   object
   array
   table
   string
   number
   date
   "true"
   "false"
   "null"

object
    '{' ws '}'
    '{' members '}'

members
    member
    member ',' members

member
    ws string ws ':' element

array
    '[' ws ']'
    '[' elements ']'

elements
    element
    element ',' elements

element
    ws value ws

table
    '---' wst newline header newline rows newline wst '---'

header
    field
    field ',' header

rows
    row
    row newline rows

field
    wst string wst
    wst string wst '.' field

row
    wst value wst
    wst value wst ',' row

string
    quoted_string
    unquoted_string

quoted_string
    '"' characters '"'

characters
    ""
    character characters

character
    '0020' . '10FFFF' - '"' - '\'
    '\' escape

escape
    '"'
    '\'
    '/'
    'b'
    'f'
    'n'
    'r'
    't'
    'u' hex hex hex hex

unquoted_string
    unquoted_start
    unquoted_start unquoted_end
    unquoted_start unquoted_chars unquoted_end

unquoted_start
    unquoted_char - wst - digit

unquoted_end
    unquoted_char - wst

unquoted_chars
    unquoted_char
    unquoted_char unquoted_chars

unquoted_char
    '0020' . '10FFFF' - delimiter

delimiter
    '"'
    ','
    '.'
    ':'
    '-'
    '['
    ']'
    '{'
    '}'
    newline

hex
    digit
    'A' . 'F'
    'a' . 'f'

number
    integer fraction exponent

integer
    digit
    onenine digits
    '-' digit
    '-' onenine digits

digits
    digit
    digit digits

digit
    '0'
    onenine

onenine
    '1' . '9'

fraction
    ""
    '.' digits

exponent
    ""
    'E' sign digits
    'e' sign digits

sign
    ""
    '+'
    '-'

date
    date 'T' time 'Z'
    date 'T' time '.' milliseconds 'Z'

date
    digit digit digit digit '-' digit digit '-' digit digit

time
    digit digit '-' digit digit '-' digit digit

milliseconds
    digit digit digit

ws
    ""
    space ws
    tab ws
    newline ws
    carriagereturn ws

wst
    ""
    space ws
    tab ws

space
    '0020'

tab
    '0009'

newline
    '000A'

carriagereturn
    '000D'
